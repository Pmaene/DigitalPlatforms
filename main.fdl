dp adder_dp (in sel : ns(1); in a, b : ns(1026); out s: ns(1027)) {
    sig b_eff : ns(1026);

    always {
        b_eff = sel == 1 ? (~b + 1) : b;
        s = a + b_eff;
    }
}

dp montgomery_dp (in a, b, m : ns(1024); in start : ns(1); out r : ns(1027))
{
    reg counter : ns(11);
    reg a_reg   : ns(1024);
    reg r_reg   : ns(1027);

    sig r_tmp   : ns(1027);
    sig sel     : ns(1);
    sig x       : ns(1026);
    sig y       : ns(1026);

    use adder_dp(sel, x, y, r_tmp);

    sfg init {
        counter = 0;
        r = 0;
        r_reg = 0;

        x = 0;
        y = 0;
        sel = 0;

        a_reg = a;
    }

    sfg count_inc {
        counter = counter + 1;
        a_reg = a_reg >> 1;
    }

    sfg count_rst {
        counter = 0;
    }

    sfg sum {
        x = r_reg;
        y = a_reg[0] == 0 ? 0 : b;
        sel = 0;
        r_reg = r_tmp;
    }

    sfg just_divide {
        sel = 0;
        x = 0;
        y = 0;
        r_reg = r_reg >> 1;
    }

    sfg sum_and_divide {
        x = r_reg;
        y = b;
        sel = 0;
        r_reg = r_tmp >> 1;
    }

    sfg sum_m_and_divide {
        x = r_reg;
        y = m;
        sel = 0;
        r_reg = r_tmp >> 1;
    }

    sfg equal_to_zero {
        sel = 0;
        x = 0;
        y = 0;
        r_reg = r_reg >> 1;
    }

    sfg not_equal_to_zero {
        sel = 0;
        x = r_reg;
        y = m;
        r_reg = r_tmp >> 1;
    }

    sfg overflow_check {
        sel = 1;
        x = r_reg;
        y = r_reg >= m ? m : 0;
        r_reg = r_tmp;
    }

    sfg stop {
        sel = 0;
        x = 0;
        y = 0;
        r = r_reg;
    }

    sfg reset_r {
        r = 0;
    }
}

fsm montgomery (montgomery_dp)
{
    initial init;
    state wait, do, if_else, stop;

    @init       (init) -> wait;
    @wait       if (start == 0) then
                    (init) -> wait;
                else
                    (init) -> do;
    @do         if (counter >= 1024) then
                    (count_rst, overflow_check, reset_r) -> stop;
                else if (r_reg[0] == 0 & a_reg[0] == 0) then
                    (just_divide, count_inc, reset_r) -> do;
                else if (r_reg[0] == 0 & a_reg[0] == 1 & b[0] == 0) then
                    (sum_and_divide, count_inc, reset_r) -> do;
                else if (r_reg[0] == 1 & a_reg[0] == 1 & b[0] == 1) then
                    (sum_and_divide, count_inc, reset_r) -> do;
                else if (r_reg[0] == 1 & a_reg[0] == 0) then
                    (sum_m_and_divide, count_inc, reset_r) -> do;
                else
                    (sum, reset_r) -> if_else;
    @if_else    if (r_reg[0] == 0) then
                    (equal_to_zero, count_inc, reset_r) -> do;
                else
                    (not_equal_to_zero, count_inc, reset_r) -> do;
    @stop       (stop) -> wait;
}

dp decoder_dp(in instruction, data_in, result_in : ns(8); out sync, result_out, address: ns(9); out data_wr, result_wr : ns(1)) {
    reg counter_reg : ns(9);
    reg instruction_reg, data_in_reg, result_in_reg : ns(8);

    reg a_reg, b_reg, m_reg : ns(1024);
    reg r_reg : ns(1027);

    sig a_sig, b_sig, m_sig : ns(1024);
    sig r_sig : ns(1027);
    sig start_sig : ns(1);

    use montgomery_dp(a_sig, b_sig, m_sig, start_sig, r_sig);

    sfg decode {
        instruction_reg = instruction;
        data_in_reg = data_in;
        result_in_reg = result_in;
    }

    sfg no_montgomery {
        a_sig = 0;
        b_sig = 0;
        m_sig = 0;
        start_sig = 0;
    }

    sfg no_sync {
        sync = 0;
    }

    sfg no_write {
        data_wr = 0;
        result_wr = 0;
        result_out = 0;
    }

    sfg no_address {
        address = 0;
    }

    sfg read_a {
        address = 0x80 - counter_reg;
        counter_reg = counter_reg+1;
        a_reg = a_reg << 8 | data_in;
    }

    sfg read_a_reg {
        a_reg = r_reg;
    }

    sfg read_b {
        address = 0x181 - counter_reg;
        counter_reg = counter_reg+1;
        b_reg = b_reg << 8 | data_in;
    }

    sfg read_b_reg {
        b_reg = r_reg;
    }

    sfg read_m {
        address = 0x281 - counter_reg;
        counter_reg = counter_reg+1;
        m_reg = m_reg << 8 | data_in;
    }

    sfg read_result {
        address = 0x80 - counter_reg;
        counter_reg = counter_reg+1;
        r_reg = r_reg << 8 | result_in;
    }

    sfg read_done {
        sync = 1;
    }

    sfg montgomery {
        a_sig = a_reg;
        b_sig = b_reg;
        m_sig = m_reg;
        r_reg = r_sig;

        $display("Result: ", r_reg);
    }

    sfg write_result {
        address = counter_reg;
        counter_reg = counter_reg+1;
        result_out = r_reg >> (8*address);
        data_wr = 0;
        result_wr = 1;
    }

    sfg write_done {
        sync = 1;
    }

    sfg reset_counter {
        counter_reg = 0;
    }

    sfg start_montgomery {
        start_sig = 1;
    }

    sfg stop_montgomery {
        start_sig = 0;
    }
}

fsm decoder(decoder_dp) {
    initial init;
    state do, done, read_all, read_one, read_result, read_done, montgomery, write_result, write_done;

    @init           (no_montgomery, no_sync, no_write, no_address, decode) -> do;
    @do             if (instruction_reg == 0x00) then
                        (no_montgomery, no_sync, no_write, no_address, decode) -> do;
                    else if (instruction_reg == 0x10) then
                        (reset_counter, no_montgomery, no_sync, no_write, no_address, decode) -> read_all;
                    else if (instruction_reg == 0x11) then
                        (reset_counter, read_b_reg, no_montgomery, no_sync, no_write, no_address, decode) -> read_one;
                    else if (instruction_reg == 0x12) then
                        (read_a_reg, read_b_reg, no_montgomery, no_sync, no_write, no_address, decode) -> do;
                    else if (instruction_reg == 0x13) then
                        (reset_counter, no_montgomery, no_sync, no_write, no_address, decode) -> write_result;
                    else if (instruction_reg == 0x14) then
                        (reset_counter, no_montgomery, no_sync, no_write, no_address, decode) -> read_result;
                    else if (instruction_reg == 0x15) then
                        (stop_montgomery, montgomery, no_sync, no_write, no_address, decode) -> montgomery;
                    else
                        (no_montgomery, no_sync, no_write, no_address, decode) -> do;
    @read_all       if (counter_reg < 0x81) then
                        (read_a, no_montgomery, no_sync, no_write, decode) -> read_all;
                    else if (counter_reg >= 0x81 & counter_reg < 0x101) then
                        (read_b, no_montgomery, no_sync, no_write, decode) -> read_all;
                    else if (counter_reg >= 0x101 & counter_reg < 0x181) then
                        (read_m, no_montgomery, no_sync, no_write, decode) -> read_all;
                    else
                        (read_done, no_montgomery, no_write, no_address, decode) -> read_done;
    @read_one       if (counter_reg < 0x81) then
                        (read_a, no_montgomery, no_sync, no_write, decode) -> read_one;
                    else
                        (read_done, no_montgomery, no_write, no_address, decode) -> read_done;
    @read_result    if (counter_reg < 0x81) then
                        (read_result, no_montgomery, no_sync, no_write, decode) -> read_result;
                    else
                        (read_done, no_montgomery, no_write, no_address, decode) -> read_done;
    @read_done      if (instruction_reg == 0x01) then
                        (no_montgomery, no_sync, no_write, no_address, decode) -> do;
                    else
                        (read_done, no_montgomery, no_write, no_address, decode) -> read_done;
    @montgomery     if (r_reg > 0) then
                        (no_montgomery, no_sync, no_write, no_address, decode) -> do;
                    else
                        (start_montgomery, montgomery, no_sync, no_write, no_address, decode) -> montgomery;
    @write_result   if (counter_reg < 0x81) then
                        (no_montgomery, no_sync, write_result, decode) -> write_result;
                    else
                        (no_montgomery, no_write, no_address, write_done, decode) -> write_done;
    @write_done     if (instruction_reg == 0x01) then
                        (no_montgomery, no_write, no_address, no_sync, decode) -> do;
                    else
                        (no_montgomery, no_write, no_address, write_done, decode) -> write_done;
}

ipblock my8051 {
    iptype "i8051system";
    ipparm "exec=main.ihx";
    ipparm "verbose=1";
}

ipblock my8051_sync(in data : ns(8)) {
    iptype "i8051systemsink";
    ipparm "core=my8051";
    ipparm "port=P1";
}

ipblock my8051_instruction(out data : ns(8)) {
    iptype "i8051systemsource";
    ipparm "core=my8051";
    ipparm "port=P0";
}

ipblock my8051_data(in idata : ns(8); out odata : ns(8); in address : ns(9); in wr : ns(1)) {
    iptype "i8051buffer";
    ipparm "core=my8051";
    ipparm "xbus=0x2000";
    ipparm "xrange=0x181";
}

ipblock my8051_result(in idata : ns(8); out odata : ns(8); in address : ns(9); in wr : ns(1)) {
    iptype "i8051buffer";
    ipparm "core=my8051";
    ipparm "xbus=0x3000";
    ipparm "xrange=0x81";
}

dp defaults (out data_in : ns(8))
{
    sfg do_always
    {
        data_in = 0;
    }
}

hardwired defaults_hw (defaults) { do_always; }

dp montgomery_sys
{
    sig sync, data_in, data_out, result_in, result_out : ns(8);
    sig instruction : ns(8);
    sig address : ns(9);
    sig data_wr, result_wr : ns(1);

    use defaults(data_in);

    use my8051;
    use my8051_sync(sync);
    use my8051_instruction(instruction);
    use my8051_data(data_in, data_out, address, data_wr);
    use my8051_result(result_in, result_out, address, result_wr);

    use decoder_dp(instruction, data_out, result_out, sync, result_in, address, data_wr, result_wr);
}

system S
{
    montgomery_sys;
}
